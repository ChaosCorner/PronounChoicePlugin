plugins {
    id 'java'
}

group = 'xyz.chaoscorner'
version = '2.0-SNAPSHOT'

repositories {
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url = 'https://papermc.io/repo/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
}

def paperMcVersion = '1.18.1-R0.1-SNAPSHOT'
def placeholderApiVersion = '2.11.1'
def mysqlConnectorVersion = '8.0.28'

dependencies {
    compileOnly "io.papermc.paper:paper-api:${paperMcVersion}"
    compileOnly "me.clip:placeholderapi:${placeholderApiVersion}"
    compileOnly "mysql:mysql-connector-java:${mysqlConnectorVersion}"
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
